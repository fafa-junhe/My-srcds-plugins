/* Plugin Template generated by Pawn Studio */
#pragma semicolon 1

#include <sourcemod>

#undef REQUIRE_PLUGIN
#include <sourcebans>

#define VERSION "1.1"

public Plugin:myinfo = 
{
	name = "Name Change Punisher",
	author = "Powerlord",
	description = "If a user changes names too many many times within a certain amount of time of each other, kick or ban them.",
	version = VERSION,
	url = "https://forums.alliedmods.net/showthread.php?t=161320"
}

// These are synchronized arrays
new g_LastNameChange[MAXPLAYERS+1];
new g_NameChangeCount[MAXPLAYERS+1];

new bool:g_UseSourcebans = false;

new Handle:g_Cvar_PunishMode = INVALID_HANDLE;
new Handle:g_Cvar_Detections = INVALID_HANDLE;
new Handle:g_Cvar_DetectionTime = INVALID_HANDLE;
new Handle:g_Cvar_BanLength = INVALID_HANDLE;
new Handle:g_Cvar_Debug = INVALID_HANDLE;

public OnPluginStart()
{
	g_Cvar_Detections = CreateConVar("ncp_detections", "5", "Number of detections before taking action", FCVAR_NONE, true, 3.0, true, 20.0);
	g_Cvar_DetectionTime = CreateConVar("ncp_detectiontime", "10", "Max length of time in seconds between detections before counter resets", FCVAR_NONE, true, 5.0, true, 60.0);
	g_Cvar_PunishMode = CreateConVar("ncp_punishmode", "1", "Punish mode. 0 is Kick, 1 is Ban.", FCVAR_NONE, true, 0.0, true, 1.0);
	g_Cvar_BanLength = CreateConVar("ncp_banlength", "60", "If ncp_punishmode is 1, how many minutes to ban for? 0 means indefinitely.", FCVAR_NONE, true, 0.0);
	g_Cvar_Debug = CreateConVar("ncp_debug", "1", "Show debugging messages.", FCVAR_NONE, true, 0.0, true, 1.0);
	
	CreateConVar("ncp_version", VERSION, "Name Change Punisher version", FCVAR_REPLICATED | FCVAR_DONTRECORD | FCVAR_SPONLY);
	AutoExecConfig(true, "namechangepunisher");
	HookEvent("player_changename", OnNameChange);
	LoadTranslations("namechangepunisher.phrases");
}

// One of these two is necessary, the other is redundant.  However, do both just in case.
public OnClientPutInServer(client)
{
	ResetClient(client);
}

// One of these two is necessary, the other is redundant.  However, do both just in case.
public OnClientDisconnect(client)
{
	ResetClient(client);
}

public OnAllPluginsLoaded()
{
	if (LibraryExists("sourcebans"))
	{
		g_UseSourcebans = true;
	}
}

public OnLibraryAdded(const String:name[])
{
	if (StrEqual("sourcebans", name))
	{
		g_UseSourcebans = true;
	}
}

public OnLibraryRemoved(const String:name[])
{
	if (StrEqual("sourcebans", name))
	{
		g_UseSourcebans = false;
	}
}


ResetClient(client)
{
	g_LastNameChange[client] = 0;
	g_NameChangeCount[client] = 0;
}

public Action:OnNameChange(Handle:event, const String:name[], bool:dontBroadcast)
{
	new userId = GetEventInt(event, "userid");
	new client = GetClientOfUserId(userId);

	if (IsClientInGame(client) && !IsFakeClient(client))
	{
		// Store it to avoid race condition for logging
		new detectionTime = GetConVarInt(g_Cvar_DetectionTime);
		new time = GetTime();
		decl String:userName[MAX_NAME_LENGTH];
		GetEventString(event, "newname", userName, sizeof(userName));

		if (time <= g_LastNameChange[client] + detectionTime)
		{
			if (++g_NameChangeCount[client] >= GetConVarInt(g_Cvar_Detections))
			{
				// The number of name changes is too great
				if (GetConVarBool(g_Cvar_PunishMode))
				{
					// Ban
					decl String:banReason[100];
					decl String:kickReason[100];
					Format(banReason, sizeof(banReason), "%T", "Ban Reason", LANG_SERVER);
					Format(kickReason, sizeof(kickReason), "%T", "Kick Reason", client);
					if (g_UseSourcebans)
					{
						SBBanPlayer(0, client, GetConVarInt(g_Cvar_BanLength), banReason);
					}
					else
					{
						BanClient(client, GetConVarInt(g_Cvar_BanLength), BANFLAG_AUTO, banReason, kickReason, "ncp");
					}
					
					if (GetConVarBool(g_Cvar_Debug))
					{
						LogMessage("Banned %s (%d) for having %d name changes in %d seconds.", userName, userId, g_NameChangeCount[client], detectionTime);
					}
				}
				else
				{
					// Kick
					KickClient(client, "%t", "Kick Reason");
					if (GetConVarBool(g_Cvar_Debug))
					{
						LogMessage("Kicked %s (%d) for having %d name changes in %d seconds.", userName, userId, g_NameChangeCount[client], detectionTime);
					}
				}
			}
			else
			{
				g_LastNameChange[client] = time;

				if (GetConVarBool(g_Cvar_Debug))
				{
					LogMessage("%s (%d) changed names %d times in the last %d seconds.", userName, userId, g_NameChangeCount[client], detectionTime);
				}
			}

		}
		else
		{

			g_NameChangeCount[client] = 1;
			g_LastNameChange[client] = time;
			if (GetConVarBool(g_Cvar_Debug))
			{
				LogMessage("%s (%d) changed names for the first time in this %d second time period.", userName, userId, detectionTime);
			}
		}

	}

	return Plugin_Continue; // Not necessary as this is a Post Event, but good form
}
